/* --------------------------------------------------------------
   Two-compartment reduced model for neocortical electrogenesis
   DEMO
  
   Z. F. Mainen and T. J. Sejnowski (1996) Influence of dendritic
   structure on firing pattern in model neocortical neurons. 
   Nature 382: 363-366. 

   author:
   Zach Mainen
   zach@salk.edu
 
 -------------------------------------------------------------- */



print " "
print " << defining template for two-compartment mPYr cell >> "
print " "


begintemplate mPYr		// create a new template object
public soma, dend

// --------------------------------------------------------------
// passive properties
// --------------------------------------------------------------

v_init    = -70      // resting membrane potential (approximate) (mV)
forall cm = 0.75     // membrane capacity (uF-cm^-2
rm        = 30000    // membrane resistivity (ohm-cm^2)
celsius   = 37
ra 		=  150
rho = 165 // dendritic to axo-somatic area ratio 
                     // useful range approx (50 to 200)
kappa = 10 // coupling resistance (Mohm) 
                     // useful range approx (0 to 10)

create soma[1]		// axo-somatic compartment
create dend[1]		// dendritic compartment
access soma
connect dend(0), soma(.5)  


proc init() { local v_potassium, v_sodium

  v_potassium = -100		// potassium reversal potential 
  v_sodium = 50			// sodium reversal potential 
  
  
  dend {
    diam = 10/PI          
    L  = rho*soma.L          // dend area is axon area multiplied by rho
    Ra = Ra*kappa/ri(.5)     // axial resistivity is adjusted to achieve
                             // desired coupling resistance

                     // uses the associated ".mod" files
    insert na        // na.mod
    insert km        // km.mod
    insert kca       // kca.mod
    insert ca        // ca.mod
    insert cad2       // cad2.mod
    insert pas     
	// insert Nap		// nap.mod inserted in (Bazhenov et al.,2002)
	// insert kleak 
  }
  
  soma {
	diam = 10/PI
	L = 10			// area of 100 um^2

    // note Ra of axon is irrelevant to the 2-comp simulation

    insert na         // na.mod
    insert kv         // kv.mod
	// insert Nap		// nap.mod inserted in (Bazhenov et al.,2002)
  }

  v_init    = -70      // resting membrane potential (approximate) (mV)
  forall cm = 0.75     // membrane capacity (uF-cm^-2
  forall Ra = 150
  rm        = 30000    // membrane resistivity (ohm-cm^2)
  dend.g_pas = 1/rm    // only dendrite has leak conductance
  dend.e_pas = v_init  // leak reversal potential (mV)
  //dend.Ra = Ra*kappa/ri(.5)
  
  // --------------------------------------------------------------
  // active conductances
  // --------------------------------------------------------------
  
  // Axo-somatic conductance densities (pS-um^-2)
  soma.gbar_na = 3 //30000     // fast Na+ 
  soma.gbar_kv = 0.2 //1500      // fast non-inactivating K+ 
  //soma.gbar_nap = 7e-5      // Persistent Na

  // Dendritic conductance densities (pS-um^-2)
  // dend.gbar_nap = 7e-5		// Nap inserted in (Bazhenov et al.,2002)
  // dend.gmax = 2.5e-6		// 0âˆ’0.0025 mS/cm2 kelak inserted in (Bazhenov et al.,2002)
  dend.gbar_na = 1.5e-3         // fast Na+ 
  dend.gbar_ca = 1e-5         // high voltage-activated Ca^2+ 
  dend.gbar_km = 1e-5         // slow voltage-dependent non-inactivating K+
  dend.gbar_kca = 0.3e-3         // slow Ca^2+-activated K+

  forall ek = -90      // K+ current reversal potential (mV)
  forall ena = 60      // Na+ current reversal potential (mV)

  dend.eca = 140       // Ca2+ current reversal potential (mV)
                     // using an ohmic current rather than GHK equation
  dend { ion_style("ca_ion",0,1,0,0,0) }  

  print " "
  print "<< mPYr: for axosomatic compartment fast INa, fast IKv, INa(p) >>"
  print "<< mPYr: for dendritic  compartment Ikm, IKCa, IHVA, IKL, INa(p) >>"
  print " "

}
endtemplate mPYr



